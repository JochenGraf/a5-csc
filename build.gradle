buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "http://files.basex.org/maven" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.8.2"
        classpath "org.grails.plugins:hibernate5:6.0.4"
    }
}

version "0.9"
group "a5"

ext {
    grailsApps = ['a5-core', 'a5-slave-ffmpeg']
}

subprojects { project ->
    println project.name
    boolean isGrailsApp = grailsApps.contains(project.name)
    boolean isGrailsProject = isGrailsApp

    if (isGrailsProject) {
        apply plugin:"eclipse"
        apply plugin:"idea"
        if (isGrailsApp) {
            apply plugin:"war"
            apply plugin:"org.grails.grails-web"
            apply plugin:"org.grails.grails-gsp"
            apply plugin:"asset-pipeline"
        }
        repositories {
            mavenLocal()
            maven { url "https://repo.grails.org/grails/core" }
            maven { url "http://files.basex.org/maven" }
        }
        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }
        dependencies {
            compile "org.springframework.boot:spring-boot-starter-logging"
            compile "org.springframework.boot:spring-boot-autoconfigure"
            compile "org.grails:grails-core"
            console "org.grails:grails-console"
        }
        if (isGrailsApp) {
            dependencies {
                compile "org.springframework.boot:spring-boot-starter-actuator"
                compile "org.springframework.boot:spring-boot-starter-tomcat"
                compile "org.grails:grails-dependencies"
                compile "org.grails:grails-web-boot"
                compile "org.grails.plugins:cache"
                compile "org.grails.plugins:scaffolding"
                compile "org.grails.plugins:hibernate5"
                compile "org.hibernate:hibernate-core:5.1.2.Final"
                compile "org.hibernate:hibernate-ehcache:5.1.2.Final"
                profile "org.grails.profiles:web"
                runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.11.6"
                runtime "com.h2database:h2"
                testCompile "org.grails:grails-plugin-testing"
                testCompile "org.grails.plugins:geb"
                testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
                testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
            }
        }
    }
    afterEvaluate {
        if (isGrailsProject) {
            assets {
                minifyJs = true
                minifyCss = true
            }
        }
    }
}